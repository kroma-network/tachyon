load("@crate_index//:defs.bzl", "aliases", "all_crate_deps")
load("@cxx.rs//tools/bazel:rust_cxx_bridge.bzl", "rust_cxx_bridge")
load("@local_config_cuda//cuda:build_defs.bzl", "if_cuda")
load("//bazel:tachyon.bzl", "if_gpu_is_configured", "if_has_openmp")
load("//bazel:tachyon_cc.bzl", "tachyon_cc_library")
load("//bazel:tachyon_rust.bzl", "tachyon_rust_library", "tachyon_rust_test")

FEATUERS = if_gpu_is_configured(["gpu"])

tachyon_rust_library(
    name = "halo2",
    srcs = glob(["src/**/*.rs"]),
    aliases = aliases(),
    crate_features = FEATUERS,
    proc_macro_deps = all_crate_deps(proc_macro = True),
    deps = all_crate_deps(normal = True) + [
        ":bn254_blake2b_writer",
        ":bn254_cxx_bridge",
        ":bn254_evals",
        ":bn254_msm",
        ":bn254_msm_gpu",
        ":bn254_poly",
        ":bn254_rational_evals",
        ":bn254_shplonk_prover",
        ":bn254_shplonk_proving_key",
        ":xor_shift_rng",
        ":xor_shift_rng_cxx_bridge",
        "//tachyon/rs:tachyon_rs",
    ],
)

# NOTE(chokobole): Maybe we can add this attribute to `halo2_test`.
# But this approach doesn't work when compiling with nvcc.
# rustc_flags = if_has_openmp(["-lgomp"]),
cc_library(
    name = "openmp",
    linkopts = if_has_openmp(["-fopenmp"]),
)

tachyon_rust_test(
    name = "halo2_test",
    size = "small",
    aliases = aliases(),
    crate = ":halo2",
    crate_features = FEATUERS,
    proc_macro_deps = all_crate_deps(proc_macro_dev = True),
    deps = all_crate_deps(normal_dev = True) + [
        ":openmp",
        "@local_config_gmp//:gmp",
    ] + if_cuda([
        "@local_config_cuda//cuda:cudart_static",
    ]),
)

rust_cxx_bridge(
    name = "bn254_cxx_bridge",
    src = "src/bn254.rs",
    args = if_gpu_is_configured([
        '--cfg=feature="gpu"',
    ]),
    deps = [":bn254_api_hdrs"],
)

rust_cxx_bridge(
    name = "xor_shift_rng_cxx_bridge",
    src = "src/xor_shift_rng.rs",
    deps = [":xor_shift_rng_api_hdrs"],
)

tachyon_cc_library(
    name = "bn254_api_hdrs",
    hdrs = [
        "include/bn254_blake2b_writer.h",
        "include/bn254_evals.h",
        "include/bn254_msm.h",
        "include/bn254_msm_gpu.h",
        "include/bn254_poly.h",
        "include/bn254_rational_evals.h",
        "include/bn254_shplonk_prover.h",
        "include/bn254_shplonk_proving_key.h",
        "include/proving_key_impl_forward.h",
    ],
    deps = ["@cxx.rs//:core"],
)

tachyon_cc_library(
    name = "bn254_blake2b_writer",
    srcs = [
        "src/blake2b_writer_impl.h",
        "src/bn254_blake2b_writer.cc",
    ],
    deps = [
        ":bn254_api_hdrs",
        ":bn254_cxx_bridge/include",
        "//tachyon/math/elliptic_curves/bn/bn254:g1",
        "//tachyon/rs/base:container_util",
        "//tachyon/zk/plonk/halo2:blake2b_transcript",
    ],
)

tachyon_cc_library(
    name = "bn254_evals",
    srcs = [
        "src/bn254_evals.cc",
        "src/bn254_evals_impl.h",
    ],
    deps = [
        ":bn254_api_hdrs",
        ":bn254_cxx_bridge/include",
        ":degrees",
        "//tachyon/math/elliptic_curves/bn/bn254:g1",
        "//tachyon/math/polynomials/univariate:univariate_evaluations",
    ],
)

tachyon_cc_library(
    name = "bn254_msm",
    srcs = ["src/bn254_msm.cc"],
    deps = [
        ":bn254_api_hdrs",
        ":bn254_cxx_bridge/include",
        "//tachyon/c/math/elliptic_curves/bn/bn254:msm",
    ],
)

tachyon_cc_library(
    name = "bn254_msm_gpu",
    srcs = if_gpu_is_configured(["src/bn254_msm_gpu.cc"]),
    deps = [
        ":bn254_api_hdrs",
        ":bn254_cxx_bridge/include",
        "//tachyon/c/math/elliptic_curves/bn/bn254:msm_gpu",
    ],
)

tachyon_cc_library(
    name = "bn254_poly",
    srcs = [
        "src/bn254_poly.cc",
        "src/bn254_poly_impl.h",
    ],
    deps = [
        ":bn254_api_hdrs",
        ":bn254_cxx_bridge/include",
        ":degrees",
        "//tachyon/c/math/elliptic_curves/bn/bn254:g1",
        "//tachyon/math/polynomials/univariate:univariate_polynomial",
    ],
)

tachyon_cc_library(
    name = "bn254_rational_evals",
    srcs = [
        "src/bn254_rational_evals.cc",
        "src/bn254_rational_evals_impl.h",
    ],
    deps = [
        ":bn254_api_hdrs",
        ":bn254_cxx_bridge/include",
        ":degrees",
        "//tachyon/math/base:rational_field",
        "//tachyon/math/elliptic_curves/bn/bn254:g1",
        "//tachyon/math/polynomials/univariate:univariate_evaluations",
    ],
)

tachyon_cc_library(
    name = "bn254_shplonk_prover",
    srcs = ["src/bn254_shplonk_prover.cc"],
    deps = [
        ":bn254_api_hdrs",
        ":bn254_cxx_bridge/include",
        ":bn254_evals",
        ":bn254_poly",
        ":bn254_rational_evals",
        ":bn254_shplonk_prover_impl",
        ":bn254_shplonk_proving_key_impl",
        "//tachyon/base/buffer",
        "//tachyon/math/elliptic_curves/msm:variable_base_msm",
        "//tachyon/math/polynomials/univariate:univariate_evaluation_domain_factory",
        "//tachyon/rs/base:container_util",
        "//tachyon/rs/base:rust_vec_copyable",
    ],
)

tachyon_cc_library(
    name = "bn254_shplonk_prover_impl",
    hdrs = [
        "src/bn254_shplonk_prover_impl.h",
        "src/prover_impl.h",
    ],
    deps = [
        ":bn254_shplonk_proving_key_impl",
        ":degrees",
        "//tachyon/base/buffer",
        "//tachyon/base/functional:callback",
        "//tachyon/math/elliptic_curves/bn/bn254",
        "//tachyon/zk/base/commitments:shplonk_extension",
        "//tachyon/zk/plonk/halo2:blake2b_transcript",
        "//tachyon/zk/plonk/halo2:prover",
    ],
)

tachyon_cc_library(
    name = "bn254_shplonk_proving_key",
    srcs = ["src/bn254_shplonk_proving_key.cc"],
    deps = [
        ":bn254_api_hdrs",
        ":bn254_cxx_bridge/include",
        ":bn254_shplonk_proving_key_impl",
        "//tachyon/rs/base:container_util",
    ],
)

tachyon_cc_library(
    name = "bn254_shplonk_proving_key_impl",
    srcs = [
        "src/bn254_shplonk_proving_key_impl.h",
        "src/buffer_reader.h",
        "src/endian_auto_reset.h",
        "src/shplonk_proving_key_impl.h",
    ],
    deps = [
        ":degrees",
        "//tachyon/base:logging",
        "//tachyon/base/buffer",
        "//tachyon/base/containers:container_util",
        "//tachyon/math/elliptic_curves/bn/bn254",
        "//tachyon/math/finite_fields:prime_field_base",
        "//tachyon/math/polynomials/univariate:univariate_evaluations",
        "//tachyon/math/polynomials/univariate:univariate_polynomial",
        "//tachyon/zk/base/commitments:shplonk_extension",
        "//tachyon/zk/plonk/circuit:column_key",
        "//tachyon/zk/plonk/circuit:gate",
        "//tachyon/zk/plonk/circuit:phase",
        "//tachyon/zk/plonk/halo2:pinned_verifying_key",
        "//tachyon/zk/plonk/keys:proving_key",
        "//tachyon/zk/plonk/permutation:permutation_argument",
    ],
)

tachyon_cc_library(
    name = "degrees",
    hdrs = ["src/degrees.h"],
)

tachyon_cc_library(
    name = "xor_shift_rng_api_hdrs",
    hdrs = ["include/xor_shift_rng.h"],
    deps = ["@cxx.rs//:core"],
)

tachyon_cc_library(
    name = "xor_shift_rng",
    srcs = ["src/xor_shift_rng.cc"],
    deps = [
        ":xor_shift_rng_api_hdrs",
        ":xor_shift_rng_cxx_bridge/include",
        "//tachyon/base:logging",
        "//tachyon/c/crypto/random:rng",
        "//tachyon/rs/base:container_util",
    ],
)
