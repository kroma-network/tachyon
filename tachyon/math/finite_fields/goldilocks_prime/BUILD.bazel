load("//bazel:tachyon.bzl", "if_polygon_zkevm_backend")
load("//bazel:tachyon_cc.bzl", "tachyon_cc_library", "tachyon_cc_test")
load("//tachyon/math/finite_fields/generator/prime_field_generator:build_defs.bzl", "generate_prime_fields")

package(default_visibility = ["//visibility:public"])

# 2^64 - 2^32 + 1
# Hex: 0xffffffff00000001
GOLDILOCKS_MODULUS = "18446744069414584321"

generate_prime_fields(
    name = "goldilocks",
    class_name = "Goldilocks",
    hdr_include_override = """#include "tachyon/export.h"
#if defined(TACHYON_GMP_BACKEND)
#include "tachyon/math/finite_fields/prime_field_gmp.h"
#endif  // defined(TACHYON_GMP_BACKEND)
#if defined(TACHYON_POLYGON_ZKEVM_BACKEND)
#include "tachyon/math/finite_fields/goldilocks_prime/prime_field_goldilocks.h"
#include "tachyon/build/build_config.h"
#endif  // defined(TACHYON_POLYGON_ZKEVM_BACKEND)""",
    modulus = GOLDILOCKS_MODULUS,
    namespace = "tachyon::math",
    special_prime_override = """#if defined(TACHYON_POLYGON_ZKEVM_BACKEND)
#if ARCH_CPU_X86_64
  constexpr static bool kIsSpecialPrime = true;
  constexpr static bool kIsGoldilocks = true;
#else
  #error Goldilocks backend is only supported on x86_64 platform.
#endif
#else
  constexpr static bool kIsSpecialPrime = false;
#endif""",
    deps = if_polygon_zkevm_backend([
        ":prime_field_goldilocks",
        "//tachyon/build:build_config",
    ]),
)

tachyon_cc_library(
    name = "prime_field_goldilocks",
    hdrs = ["prime_field_goldilocks.h"],
    defines = ["TACHYON_POLYGON_ZKEVM_BACKEND"],
    deps = [
        "//tachyon/base:random",
        "//tachyon/base/strings:string_number_conversions",
        "//tachyon/base/strings:string_util",
        "//tachyon/math/base/gmp:gmp_util",
        "//tachyon/math/finite_fields:prime_field_base",
        "//tachyon/math/finite_fields:prime_field_forward",
    ] + if_polygon_zkevm_backend([
        "@polygon_zkevm_goldilocks//:base_field",
    ]),
)

tachyon_cc_test(
    name = "goldilocks_prime_unittests",
    size = "small",
    srcs = if_polygon_zkevm_backend(["prime_field_goldilocks_unittest.cc"]),
    deps = [":goldilocks"],
)
